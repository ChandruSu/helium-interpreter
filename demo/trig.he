? --------------------------------
    Title:      Trigonometric Ratio Module
    Author:     Chandru Suresh
    Date:       21.07.2022
  -------------------------------- ?
  
math <- {
    "PI": 3.14159265
}

math.toRadians <- $(deg) { return deg * math.PI / 180.0 }

math.toDegrees <- $(rad) { return rad * 180.0 / math.PI }

# sine function implementation using maclaurin series
math.sin <- $(rad) {
    loop rad > math.PI {
        rad <- rad - 2 * math.PI
    }

    loop rad < -math.PI {
        rad <- rad + 2 * math.PI
    }

    i <- 1
    n <- 1
    v <- rad
    m <- -1

    loop i < 15 {
        i <- i + 1
        n <- n * i
        if i % 2 {
            v <- v + m * (@pow(rad, i) / n)
            m <- -m
        }
    }

    return v
}

# cosine function implementation using maclaurin series
math.cos <- $(rad) {
    loop rad > math.PI {
        rad <- rad - 2 * math.PI
    }

    loop rad < -math.PI {
        rad <- rad + 2 * math.PI
    }

    i <- 1
    n <- 1
    v <- 1
    m <- -1

    loop i < 15 {
        i <- i + 1
        n <- n * i
        if i % 2 == 0 {
            v <- v + m * (@pow(rad, i) / n)
            m <- -m
        }
    }

    return v
}

math.tan <- $(rad) { return @math.sin(rad) / @math.cos(rad) }
